plugins {
    id "java-library"
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id "xyz.jpenilla.run-paper" version "2.1.0"
}

group = "org.example.exampleplugin"
version = "1.0.0"
archivesBaseName = "example-paperapi-plugin"

repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/groups/public/" }
    maven { url "https://repo.papermc.io/repository/maven-public/" }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.17.1-R0.1-SNAPSHOT'
}

shadowJar {
    minimize()
}

jar.finalizedBy(shadowJar)

processResources {
    def props = ["version": version]
    inputs.properties props
    filteringCharset "UTF-8"
    filesMatching(["plugin.yml"]) {
        expand props
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.forkOptions.jvmArgs << "-Dfile.encoding=UTF-8"
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}
